# Modelled on https://revbayes.github.io/tutorials/morph_tree/V2

# Single partition
allChars <- concatenate(neo, trans)



# Dataset properties
taxa <- allChars.names()
nTaxa <- allChars.size()
nEdge <- 2 * nTaxa - 3

moves = VectorMoves()

# Uniform prior on tree topologies
topology ~ dnUniformTopology(taxa)
moves.append( mvNNI(topology, weight = nEdge / 8.0) )
moves.append( mvSPR(topology, weight = nEdge / 30.0) )


# Compound dirichlet prior on edge length variability
# See https://revbayes.github.io/tutorials/ctmc/
# Prior mean tree length should be within a couple of orders of magnitude of the
# true value (Zhang, Rannala & Yang, 2012); one change per site should be okay?

gamma_shape <- 2
exp_steps <- 1 # Expected changes per site across the tree.
tree_length ~ dnGamma(shape = gamma_shape, rate = gamma_shape / exp_steps)
moves.append( mvScale(tree_length, weight = 1.0) )

rel_br_lengths ~ dnDirichlet( rep(1.0, nEdge) )
moves.append( mvBetaSimplex(rel_br_lengths, weight = nEdge / 3.0) )
moves.append( mvDirichletSimplex(rel_br_lengths, weight = nEdge / 20.0) )
br_lengths := rel_br_lengths * tree_length

phylogeny := treeAssembly(topology, br_lengths)


# Log-normal distributed rate variation (Wagner 2012, doi: 10.1098/rsbl.2011.0523)
rate_log_sd ~ dnGamma( 1, 1 )
moves.append( mvScale(rate_log_sd, lambda = 1, weight = 1.5) )

# Mean = 1, so that tree length ~ character changes
# 6 categories (see Wright & Wynd 2024,
# https://www.biorxiv.org/content/10.1101/2024.06.26.600858v1)
rate_categories := fnDiscretizeDistribution( dnLognormal( 0, rate_log_sd ), 6)






























matrixByState[1] <- allChars

# Partition all characters by number of states (k)
maxK = 10
idx = 1
for (k in 2:maxK) {
  matrixByState[k] <- allChars
  matrixByState[k].setNumStatesPartition(k)
  # Reduce memory footprint, per https://github.com/revbayes/revbayes/issues/552
  matrixByState[k].removeExcludedCharacters()
  nc = matrixByState[k].nchar()
  
  if (nc > 0) {
    print(nc, " characters in partition ", idx, " (", k, " states).")
    q[idx] <- fnJC(k) # Generate Q allChars based on Mk model
    m_morph[idx] ~ dnPhyloCTMC(
      tree = phylogeny,
      
      siteRates = rate_categories,
      Q = q[idx],
      type = "Standard",
      coding = "variable"
    )
    m_morph[idx].clamp(matrixByState[k])
    idx = idx + 1
  }
}

# Conduct MCMC analysis
mymodel = model(phylogeny)

monitors = VectorMonitors()
monitors.append( mnScreen(printgen = 300, prior = FALSE, posterior = FALSE) )
